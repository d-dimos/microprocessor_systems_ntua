; Συστήματα Μικροϋπολογιστών - Ροή Υ [6ο Εξάμηνο]
; Αμπατζή Ναυσικά - 031 17 198
; Δημήτρης Δήμος - 031 17 165
; 3η Ομάδα Ασκήσεων
; 1η ’σκηση

; ====== ΣΗΜΑΝΤΙΚΟ ΣΧΟΛΙΟ !!!
; Ο κώδικας έχει διαμορφωθεί με τρόπο ώστε να απεικονίζεται στα
; 7-segment-displays αρχικά το χρονόμετρο ως 00. Όταν δωθεί διακοπή,
; τότε ξεκινάει η αντίστροφη μέτρηση και τα ζητούμενα της άσκησης.
; Όταν το χρονόμετρο μηδενιστεί, μέχρι την επόμενη διακοπή
; συνεχίζει να φαίνεται χρονόμετρο ίσο με 00. Η προσθήκη έγινε
; χωρίς ουσιαστικό λόγο, απλώς φάνηκε ωραίο. Μπορεί άμεσα να αφαιρεθεί
; με αφαίρεση των 5 πρώτων σειρών μετά την ετικέτα INF και την τροποποίηση
; της εντολής JMP LO σε JMP INF.
; ===========================



; ======================== MAIN PROGRAM ========================
	
	MVI C,04H	; επαναλήπτης
	
	; === αρχικοποίηση του 7-seg-display ===
	IN 10H
MAIN:	LXI H,0A00H	
	MVI M,00H
	INX H
	MVI M,06H
	INX H
FILL:	MVI M,10H	; (0Α02) έως (0Α05) = 10Η = 4 κενά 7-seg-displays
	INX H		
	DCR C		
	JNZ FILL
	
	; ===== μάσκα διακοπών + επίτρεψη ======
	MVI A,0DH	; 0D(hex) = 00001101(bin)
	SIM
	EI
	
	; === ατέρμονος βρόχος μέχρι διακοπή ===
INF:	LXI H,0A00H
	MVI M,00H
	INX H
	MVI M,00H
LO:	CALL DISP	; μέχρι να δωθεί διακοπή, έχουμε εικονιζόμενο
			; χρονομέτρη 00 στο 7-seg-display
	JMP LO

; ===============================================================



; ================== INTERRUPT SERVICE ROUTINE ==================
	
INTR_ROUTINE:
	MVI E,3CH	; αρχικοποίηση χρονομέτρου (E) = 60
	LXI B,0032H	; (BC) = 50, δηλ. 50 ms
	EI

INIT:	MVI D,0AH	; (D) = 10, (D)*(BC) = 0.5 sec

	CALL NEXT_SEC	; ρύθμιση των θέσεων 0Α00 και 0Α01
			; σύμφωνα με τον χρονομετρητή E
	
	ANI 00H
	STA 3000H	; άναμμα των LED
L1:	CALL DISP	; απεικόνιση χρόνου
	CALL DELB	; καθυστέρηση 50 ms
	DCR D
	JNZ L1		; επανάληψη 10 φορες. Total delay = 0.5 sec
	
	MVI D,0AH	; άλλα 0.5 sec
	
	CMA
	STA 3000H	; ομοίως με πριν (σβήσιμο των LED)
L2:	CALL DISP
	CALL DELB
	DCR D
	JNZ L2
	
	DCR E
	JNZ INIT

	JMP INF

; ===============================================================



; ===================== ROUTINE TO FIX TIMER ====================

; E ---> BCD και αποθήκευση στις 0Α00, 0Α01

NEXT_SEC:
	PUSH PSW
	PUSH B
	PUSH H
	
	MVI B,FFH
	MOV A,E

L3:	INR B
	SUI 0AH
	JNC L3		; μέχρι να γίνει αρνητικό

	ADI 0AH 	; προσθέτουμε 10. Τώρα:
			; (Β) = δεκάδες
			; (Α) = μονάδες

	LXI H,0A00H
	MOV M,A	; 1ο 7-seg-disp = μονάδες
	INX H	
	MOV M,B	; 2ο 7-seg-disp = δεκάδες

	POP H
	POP B
	POP PSW
	RET

; ===============================================================



; =============== 7-SEGMENT-DISPLAY ROUTINE =====================

DISP:	PUSH PSW
	PUSH D
	LXI D,0A00H		; μετακίνηση του block 0A00H - 0A05H
				; στο σημείο που διαβάζει η DCD
	CALL STDM	
	CALL DCD
	POP D
	POP PSW
	RET
; ===============================================================

	END
; Συστήματα Μικροϋπολογιστών - Ροή Υ [6ο Εξάμηνο]
; Αμπατζή Ναυσικά - 031 17 198
; Δημήτρης Δήμος - 031 17 165
; 3η Ομάδα Ασκήσεων
; 2η ’σκηση

; === ΣΧΟΛΙΟ !!!
; Θεωρούμε ότι ο χρήστης σε κάθε διακοπή δίνει
; πρώτα το MSB και ύστερα το LSB του δεκαεξαδικου αριθμού εισόδου


; ======================== MAIN PROGRAM ========================
	
MAIN:	IN 10H		; άρση προστασίας μνήμης
	MVI A,0DH	; (Α) = 00001101. Απενεργοποίηση των διακοπών
	SIM		; πέραν της RST 6.5
	EI

	MVI B,06H
	LXI H,0A00H	
FILL:	MVI M,10H	; κενά 7-seg-displays
	INX H
	DCR B
	JNZ FILL

	; ==== κατώφλια (θεωρητικά τα θέτει ο χρήστης)
	MVI C,40H	; Κ1 = (C)
	MVI D,80H	; K2 = (D)
	MVI E,C0H	; K3 = (E)

	; =========================================
	; αυτό το κομμάτι κώδικα αποσκοπεί στο να μην εμφανίζεται τίποτα
	; στην οθόνη του monitor κατά το πάτημα του INTR
	PUSH D
	LXI D,0A00H	; μετακίνηση του block 0A00H - 0A05H
			; στο σημείο που διαβάζει η DCD
	CALL STDM
	POP D
	CALL DCD	
	; =========================================


WAIT1:	JMP WAIT1	; βρόχος αναμονής διακοπών

; ==============================================================



; ================= INTERRUPT SERVICE ROUTINE ==================

INTR_ROUTINE:

	; ===== είσοδος στον Α
	CALL KIND	; A <-- most significant δεκαεξαδικό ψηφίο
	STA 0A05H
	RLC
	RLC
	RLC
	RLC
	MOV B,A
	CALL KIND	; Α <-- less significant δεκαεξαδικό ψηφίο
	STA 0A04H
	ADD B		; (Α) = αριθμός εισόδου
	; ===========================		
	
	; =========== (Α) <= (C) ?
	CMP C
	JC FIR
	JZ FIR
	JMP NOFIR
FIR:	MVI A,08H
	JMP END

	; =========== (Α) <= (D) ?
NOFIR:	CMP D
	JC SEC
	JZ SEC
	JMP NOSEC
SEC:	MVI A,04H
	JMP END

	; =========== (Α) <= (E) ?
NOSEC:	CMP E
	JC THI
	JZ THI
	JMP NOTHI
THI:	MVI A,02H
	JMP END

	; αν τίποτα εκ των παραπάνω, τότε ανήκει στο τελευταίο διάστημα
NOTHI:	MVI A,01H

END:	CMA
	STA 3000H

	PUSH D
	LXI D,0A00H	; μετακίνηση του block 0A00H - 0A05H
			; στο σημείο που διαβάζει η DCD
	CALL STDM
	POP D
	EI		; επιτρέπουμε ξανά διακοπές

DISP:	CALL DCD	; εμφάνιση αριθμού μέχρι την επόμενη διακοπή
	JMP DISP

; ==============================================================

	END
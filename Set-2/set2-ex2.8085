; Συστήματα Μικροϋπολογιστών - Ροή Υ [6ο Εξάμηνο]
; Δημήτρης Δήμος - 031 17 165
; 2η Ομάδα Ασκήσεων
; 2η ’σκηση 


; Ο κώδικας έχει διαμορφωθεί έτσι ώστε, αν κατά την εκκίνηση
; του προγράμματος το LSB των dip switch είναι ON, να απαιτείται
; πρώτα επαναφορά του σε κατάσταση OFF και ύστερα να έπονται
; όσα ζητούνται να υλοποιηθούν.


; ============= ΚΥΡΙΟ ΠΡΟΓΡΑΜΜΑ (ΕΚΚΙΝΗΣΗ) ================
START:	LXI B,411BH	; N = (BC) = 16666 => delay = 0.2 sec
	MVI E,4BH	; E = 75 (75 x 0.2 = 15 sec)
	MVI H,00H
	MVI A,00H
	
	CALL LSB	; έλεγχοι του dip switch LSB
	JC START
L1:	CALL LSB
	JNC L1
L2:	CALL LSB
	JC L2
	
; ===== ΥΠΟΠΡΟΓΡΑΜΜΑ: ΟΤΑΝ DIP SWITCH LSB = OFF =======
GO_0:	CPI 04H	; αν A-4 >= 0, τότε άλλαξε την έξοδο (αναβόσβηνε)
	JC NEXT0
	CALL CHNG

NEXT0:	CALL PUT

	CALL LSB	; Έλεγχος LSB dip switch.
	JC GO_1	; Αν ενεργοποιήθηκε, πήγαινε στο 
			; άλλο μισό υποπρόγραμμα
	DCR E		
	JZ CHOOS
	JMP GO_0

; ===== ΥΠΟΠΡΟΓΡΑΜΜΑ: ΟΤΑΝ DIP SWITCH LSB = ON =========
GO_1:	CPI 04H	; A-4 >= 0, τότε άλλαξε έξοδο (αναβόσβηνε)
	JC NEXT1
	CALL CHNG

NEXT1:	CALL PUT

	CALL LSB	; Έλεγχος LSB dip switch.
	JC NEXT2	; Αν απενεργοποιήθηκε, ανανέωσε τον χρόνο και
	MVI E,4BH
	JMP GO_0	; ξαναπήγαινε στο πρώτο μισό υποπρόγραμμα (επανάληψη)

NEXT2:	DCR E		
	JZ CHOOS
	JMP GO_1

; ========== ΤΕΛΟΣ ΤΩΝ 15 sec + ΔΙΑΧΕΙΡΙΣΗ =============	
CHOOS:	MVI E,4BH	; Η συνάρτηση καλείται όταν μηδενίζεται το Ε,
	MVI A,FFH	; δηλαδή όταν οι 75 επαναλήψεις τελειώσουν, ώστε
	STA 3000H	; να επαναφέρει τις σωστές αρχικοποιήσεις.
	MVI A,00H	; Έτσι το πρόγραμμα, έτοιμο, θα περιμένει την
	MVI H,00H	; επανενεργοποίηση του push-button
	CALL LSB	
	JC L2		; Βάσει της κατάστασης του dip switch LSB
	JMP L1		; το πρόγραμμα θε περιμένει στο σωστό σημείο
; =======================================================


; =========== ΥΠΟΡΟΥΤΙΝΑ ΧΡΟΝΟΚΑΘΥΣΤΈΡΗΣΗΣ =============
; delay = 12(N-1) + 15.5 μsec, όπου Ν = (BC) 

DELAY:	DCX B		; 6 καταστάσεις
	MOV A,B	; 4 καταστάσεις
	ORA C		; 4 καταστάσεις
	JNZ DELAY	; 7/10 καταστάσεις
	RET		; 10 καταστάσεις
; ======================================================

; ========= ΥΠΟΡΟΥΤΙΝΑ ΕΛΕΓΧΟΥ DIP SWITCH LSB ==========	
LSB:	MOV D,A	; προσωρινό σώσιμο του Α
	LDA 2000H
	RAR		; λήψη του LSB στον CY
	MOV A,D	; επαναφορά Α
	RET
; ======================================================

; =========== ΥΠΟΡΟΥΤΙΝΑ ΕΝΑΛΛΑΓΗΣ ΤΩΝ LED =============
CHNG:	MOV A,H	; Η ρουτίνα καλείται και αντιστρέφει
	CMA		; το περιεχόμενο του Η, δηλαδή την έξοδο,
	MOV H,A	; κάθε φορά που περνούν 4 x 0.2 = 0.8 sec
	MVI A,00H	
	RET
; ======================================================

; ================ ΥΠΟΡΟΥΤΙΝΑ ΕΞΟΔΟΥ ===================
PUT:	MOV D,A	; προσωρινό σώσιμο Α
	MOV A,H	; προώθηση Η στην έξοδο
	STA 3000H	
	CALL DELAY	; πρόκληση καθυστέρησης
	MOV A,D	; επαναφορά Α
	INR A	
	RET
; ======================================================
	END